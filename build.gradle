allprojects {
    group = "io.github.jho951"

    // GitHub Actions에서 제공하는 태그/브랜치명을 가져옵니다.
    def rawVersion = System.getenv("GITHUB_REF_NAME") ?: "1.0.3-SNAPSHOT"
    // 'v1.0.4' 같은 태그가 들어오면 '1.0.4'로 변경합니다.
    version = rawVersion.startsWith("v") ? rawVersion.substring(1) : rawVersion

    repositories { mavenCentral() }
}

subprojects {
    apply plugin: "java-library"
    apply plugin: "maven-publish"

    def safeProp = { name, defaultVal = "" ->
        return System.getenv(name) ?: (project.hasProperty(name) ? project.property(name) : defaultVal)
    }

    def jVersion = safeProp('javaVersion', '17')
    def springVer = safeProp('springBootVersion', '3.3.5')
    def jjwtVer = safeProp('jjwtVersion', '0.11.5')
    def junitVer = safeProp('junit', '5.10.0')

    ext {
        springBootVersion = springVer
        jjwtVersion = jjwtVer
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(jVersion.toString().trim() as Integer)
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.release.set(jVersion.toString().trim() as Integer)
        options.encoding = "UTF-8"
    }

    dependencies {
        testImplementation platform("org.junit:junit-bom:${junitVer}")
        testImplementation 'org.junit.jupiter:junit-jupiter'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
        testImplementation 'org.assertj:assertj-core:3.24.2'
    }

    test { useJUnitPlatform() }

    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/jho951/auth")
                credentials {
                    username = System.getenv("GITHUB_ACTOR") ?: project.findProperty("gprUser")
                    password = System.getenv("GITHUB_TOKEN") ?: project.findProperty("gprKey")
                }
            }
        }

        publications {
            mavenJava(MavenPublication) {
                from components.java

                pom {
                    licenses {
                        license {
                            name = "MIT License"
                            url = "https://opensource.org/licenses/MIT"
                        }
                    }
                }
            }
        }
    }
}