allprojects {
    group = "io.github.jho951"
    version = "1.0.1"
    repositories { mavenCentral() }
}

subprojects {
    apply plugin: "java-library"
    apply plugin: "maven-publish"

    // 1. 환경 변수와 properties를 결합하여 하위 모듈에 강제로 할당
    ext {
        springBootVersion = System.getenv("springBootVersion") ?: (project.hasProperty('springBootVersion') ? project.springBootVersion : "3.3.5")
        jjwtVersion = System.getenv("jjwtVersion") ?: (project.hasProperty('jjwtVersion') ? project.jjwtVersion : "0.11.5")
        javaVersion = System.getenv("javaVersion") ?: (project.hasProperty('javaVersion') ? project.javaVersion : "17")
        junitVersion = System.getenv("junit") ?: (project.hasProperty('junit') ? project.junit : "5.10.0")
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(javaVersion.toString().trim() as Integer)
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.release.set(javaVersion.toString().trim() as Integer)
        options.encoding = "UTF-8"
    }

    dependencies {
        testImplementation platform("org.junit:junit-bom:${junitVersion}")
        testImplementation 'org.junit.jupiter:junit-jupiter'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
        testImplementation 'org.assertj:assertj-core:3.24.2'
    }

    test { useJUnitPlatform() }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifactId = "auth-${project.name}"
            }
        }
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/jho951/auth")
                credentials {
                    username = System.getenv("GITHUB_ACTOR") ?: getProperty('gprUser')
                    password = System.getenv("GITHUB_TOKEN") ?: getProperty('gprKey')
                }
            }
        }
    }
}